group = 'org.interledger'
description = 'Interledger Ledger Client'
version = '1.0.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

/* 
 custom checkstyle properties for the build.
 see https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.CheckstyleExtension.html
*/
checkstyle {
    configFile file('google_checks.xml')
    toolVersion '7.1'
    showViolations = true
    /* 
       by default, checkstyle warnings dont fail the build. but we want exactly that for circle-ci 
       pass/fail reporting, so tell the plugin to break the build on the first error 
    */
	maxWarnings = 0 
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': description,
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(":java-ilp-core")
    compile project(":java-crypto-conditions")

    compile("org.slf4j:jcl-over-slf4j:1.7.12")

    testCompile("junit:junit:4.12")    
}

//Maven compatibility task:
task writePom {
    doLast {
        pom {
            project {
                inceptionYear '2016'    
                properties {
                    "project.build.sourceEncoding" "UTF-8"
                    "maven.compiler.source" "1.8"
                    "maven.compiler.target" "1.8"
                    "checkstyle.config.location" "google_checks.xml"
                    "checkstyle.consoleOutput" "true"
                }            
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                reporting  {
                    plugins {
                        plugin {
                            groupId "org.apache.maven.plugins"
                            artifactId "maven-checkstyle-plugin"
                            version "2.17"
                        }                   
                    }
                }
            }
        }.writeTo("pom.xml")
    }
}
